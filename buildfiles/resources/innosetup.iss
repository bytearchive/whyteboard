; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{518C11F9-3A78-455B-B8A1-C1148E949DA6}
AppName=Whyteboard
SourceDir={#BASEDIR}
AppVerName=Whyteboard {#VERSION}
AppPublisher=Steven Sproat
AppPublisherURL=https://whyteboard.org
AppSupportURL=https://launchpad.net/whyteboard
AppUpdatesURL=https://launchpad.net/whyteboard
DefaultDirName={pf}\Whyteboard
DefaultGroupName=Whyteboard
AllowNoIcons=yes
LicenseFile=LICENSE.txt
InfoBeforeFile=
OutputBaseFilename=setup
Compression=lzma
SolidCompression=yes
ChangesAssociations=yes

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
;------ Copy Survey Extension (IssSurvey.dll) in your app install folder to use it on unistall
Source: buildfiles\resources\IssSurvey.dll; DestDir: {app}
;------ Copy Survey Extension Language File (IssSurvey.ini) in your app install folder
Source: buildfiles\resources\IssSurvey.ini; DestDir: {app}
;------

Source: "dist\*.*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs

; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{group}\Whyteboard"; Filename: "{app}\Whyteboard.exe"
Name: "{group}\{cm:UninstallProgram,Whyteboard}"; Filename: "{uninstallexe}"
Name: "{commondesktop}\Whyteboard"; Filename: "{app}\Whyteboard.exe"; Tasks: desktopicon

[Run]
Filename: "{app}\Whyteboard.exe"; Description: "{cm:LaunchProgram,Whyteboard}"; Flags: nowait postinstall skipifsilent

[Registry]
Root: HKCR; Subkey: ".wtbd"; ValueType: string; ValueName: ""; ValueData: "Whyteboard"; Flags: uninsdeletevalue
Root: HKCR; Subkey: "Whyteboard"; ValueType: string; ValueName: ""; ValueData: "Whyteboard Data File"; Flags: uninsdeletekey
Root: HKCR; Subkey: "Whyteboard\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\Whyteboard.exe,0"
Root: HKCR; Subkey: "Whyteboard\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\Whyteboard.exe"" --file ""%1"""


[Code]
// IssSurvey function called on uninstall
function IssSurvey(Language: PChar; ReasonsList: PChar; Server: PChar; UserName: PChar; Password: PChar): Integer;
external 'IssSurvey@{app}\IssSurvey.dll stdcall uninstallonly';

//********************************************************************************************************************************************
// IssSurvey function returns: 0 if the user has submited the comments; 1 if cancel or close pressed; 2 if ignore/skip pressed; -1 if an error occured
//
//  Language    = IssSurvey language dialog. Set this value to empty '' and default english will be used
//                ( see and include IssSurvey.ini if you need custom text or other language)
//
//  ReasonsList  = Uninstall reasons templates list. Separate multiple reasons with semicolon.
//                 Set this value to empty string '' to hide the template reasons combo box and show only the edit box
//
//  Server      = Full http path to a php script. The IssSurvey will POST at this adresss the selected uninstall reason and any user feedback/comments.
//                Ex: 'http://raz-soft.com/IssSurvey/IssSurvey_mail.php'
//                Informations posted when the user submits the survey from IssSurvey Extension:
//                $name      = the name you pass on the IssSurvey Extension function     (for security purpose)
//                $pass      = the password you pass on the IssSurvey Extension function (for security purpose)
//                $cntinfo   = the user reason/comments/feedback. !!Warning: the sent text is base64 encoded.
//                $IssSurvey = just for a check control. It will be set to 1 by IssSurvey Extension
//
//  UserName   =  a user name to submit along with the reason/comments  (for security purpose)
//
//  Password   =  a password to submit along with the reason/comments  (for security purpose)
//
//******************************************************************************************************************************************


function InitializeUninstall(): Boolean;
var
  sReasons: String;
  nCode: Integer;  { 0 if the user has submited the comments; 1 if cancel or close pressed; 2 if ignore/skip pressed; -1 if an error occured }

begin
     Result   := false;
     sReasons := 'Other reason (see below):;I do not understand how to use it;I did not like the program;I use a better application';
     nCode    := IssSurvey('en',sReasons,'http://whyteboard.org/IssSurvey_mail.php','demo','demo');

     if (nCode=0) or (nCode=2) then begin                    { submited or ignored }
          Result := true;                                    { continue uninstall setup      }
     end;

    // Unload the extension, otherwise it will not be deleted by the uninstaller
    UnloadDLL(ExpandConstant('{app}\IssSurvey.dll'));

end;